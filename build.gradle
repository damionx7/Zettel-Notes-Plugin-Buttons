Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

//apply signing config
apply from: rootProject.file(properties.getProperty("keystore.signing"))

project.ext {
    minSdk = 21
    targetSdk = 33
    keystore = rootProject.file(properties.getProperty("keystore"))
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0'
    }
}

//shared config for modules
subprojects {
    apply plugin: 'com.android.application'
    dependencies {
        implementation libs.api
        coreLibraryDesugaring libs.desugar
        testImplementation libs.bundles.junit
    }
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    android {
        compileSdk project.targetSdk

        signingConfigs {
            config {
                keyAlias PROP_KEY_ALIAS
                storeFile project.keystore
                storePassword PROP_STORE_PASSWORD
                keyPassword PROP_KEY_PASSWORD
            }
        }

        defaultConfig {
            minSdk project.minSdk
            targetSdk project.targetSdk
            signingConfig signingConfigs.config
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            debug {
                debuggable true
                minifyEnabled false
                signingConfig signingConfigs.config
            }
            release {
                signingConfig signingConfigs.config
                //disable proguard for plugin system to work
                minifyEnabled false
                debuggable false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        testOptions {
            unitTests.all {
                useJUnitPlatform()
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}