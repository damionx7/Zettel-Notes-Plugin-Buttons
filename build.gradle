import java.time.LocalDate

plugins {
    id 'com.android.application' version '8.1.3' apply false
    id 'com.android.library' version '8.1.3' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.20' apply false
}

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

//apply signing config
apply from: rootProject.file(properties.getProperty("keystore.signing"))

project.ext {
    minSdk = 21
    targetSdk = 33
    keystore = rootProject.file(properties.getProperty("keystore"))
}

//shared config for modules
subprojects { subproject ->
    //shared variables
    ext {
        packageName = ''
        versionMajor = LocalDate.now().year
        versionMinor = LocalDate.now().monthValue
        versionPatch = LocalDate.now().dayOfMonth
    }
    afterEvaluate {
        if (subproject.plugins.hasPlugin("com.android.application")) {
            //default values for subprojects

            subproject.android {
                compileSdk project.targetSdk
                namespace "${subproject.packageName}"

                signingConfigs {
                    config {
                        keyAlias PROP_KEY_ALIAS
                        storeFile project.keystore
                        storePassword PROP_STORE_PASSWORD
                        keyPassword PROP_STORE_PASSWORD
                    }
                }

                defaultConfig {
                    minSdk project.minSdk
                    targetSdk project.targetSdk

                    applicationId "${subproject.packageName}"
                    versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
                    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

                    signingConfig signingConfigs.config

                    vectorDrawables.useSupportLibrary = true
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildTypes {
                    debug {
                        debuggable true
                        minifyEnabled false
                        signingConfig signingConfigs.config
                    }
                    release {
                        signingConfig signingConfigs.config
                        //disable proguard for plugin system to work
                        minifyEnabled false
                        debuggable false
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17

                    coreLibraryDesugaringEnabled true
                }

                testOptions {
                    unitTests.all {
                        useJUnitPlatform()
                    }
                }
            }

            subproject.dependencies {
                implementation libs.api
                implementation project(path: ':base')

                coreLibraryDesugaring libs.desugar
                testImplementation libs.bundles.junit
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete buildDir
}